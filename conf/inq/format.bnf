// https://github.com/awalterschulze/katydid/blob/master/asm/asm.bnf


_lineComment : '#' {.} '\n' ;
!comment : _lineComment;

!whitespace: ' ' | '\t' | '\n' | '\r' ;
_letter : 'a'-'z' | 'A'-'Z' ;

_digit : '0'-'9' ;

_idchar : _letter | _digit | '_' | '-' | '.' | '/';

_sizecode : 'k' | 'm' | 'g' | 't' | 'p' | '%';

_uint : _digit {_digit};

_anything	: 'A'-'Z' | 'a'-'z' | '0'-'9'
		| '!' | '$' | '%' | '&' | '\'' | '*' | '+' | '-' | '/'
		| '=' | '?' | '^' | '_' | '`' | '{' | '|' | '}' | '~' | '.' | '@'
		| '\u0100'-'\U0010FFFF';
_atom : _anything {_anything} ;
_quotedpair : '\\' . ;
_quotedstring : '"' (_quotedpair | .) {_quotedpair | .} '"' ;
name : (_atom | _quotedstring) ;

operator : '>' | '<';

<< import "github.com/mperham/inspeqtor/conf/inq/ast" >>

/*

Examples:

check service mysql
  if memory(rss) > 6g for 2 cycles then alert

check service redis with port 6379
  if memory(rss) > 1g for 2 cycles then alert
  if cpu(user) > 95% for 2 cycles then alert

check host
  if cpu(user) > 400% for 2 cycles then alert
  if swap > 20% for 2 cycles then alert

# expose is an Inspeqtor Pro feature which provides a visualization UI for
# certain specific daemon subsystems.  memstats allows you to visualize
# the Go garbage collection and memory stats.
check inspeqtor
  expose memstats

*/

Check
  : "check" "service" name ParameterList RuleList              << ast.NewProcessCheck($2, $3, nil, $4), nil >>
  | "check" "service" name ParameterList ExposedList RuleList  << ast.NewProcessCheck($2, $3, $4, $5), nil >>
  | "check" "service" name ExposedList                         << ast.NewProcessCheck($2, nil, $3, nil), nil >>
  | "check" "service" name RuleList                            << ast.NewProcessCheck($2, nil, nil, $3), nil >>
  | "check" "service" name ExposedList RuleList                << ast.NewProcessCheck($2, nil, $3, $4), nil >>
  | "check" "host" ParameterList RuleList                      << ast.NewHostCheck($3, $2), nil >>
  | "check" "host" RuleList                                    << ast.NewHostCheck($2, map[string]string{}), nil >>
;

ExposedList
  : "expose" Exposed    << $1, nil >>
;

Exposed
  : name "," Exposed    << ast.AddExposed($0, $2) >>
  | name                << ast.AddExposed($0, nil) >>
;

ActionList
  : Action                   << []ast.Action{$0.(ast.Action)}, nil >>
  | Action "," ActionList    << ast.AppendAction($0, $2) >>
;

Action
  : "restart"       << ast.AddAction($0, nil) >>
  | "reload"        << ast.AddAction($0, nil) >>
  | "alert"         << ast.AddAction($0, nil) >>
  | "alert" name    << ast.AddAction($0, $1) >>
;

ParameterList
  : "with" Parameters   << $1, nil >>;

Parameters
  : name name "," Parameters      << ast.AddParam($0, $1, $3) >>
  | name name                     << ast.AddParam($0, $1, nil) >>;

IntAmount
  : name            << ast.ToInt64($0) >>;

HumanAmount
  : name      << ast.HumanAmount($0) >>
  ;

// Metrics are named like "family:name", e.g. memory:rss, cpu:user
// or mysql:Questions.
//
// Name is optional to allow English-like naming for special metrics
// like "swap" that are just a single number, otherwise we'd have to
// force awkward naming like "swap:usage".
//
// Allow metric names to begin with numbers so we can support
// metrics like "load:1".
Metric
  : name                    << ast.Metric($0, nil) >>
  | name ":" name           << ast.Metric($0, $2) >>
  | name "(" name ")"       << ast.Metric($0, $2) >>
  ;

Rule
  : "if" Metric operator HumanAmount "then" ActionList  << ast.NewRule($1, $2, $3, $5, ast.OneCycle), nil >>
  | "if" Metric operator HumanAmount "for" IntAmount "cycles" "then" ActionList  << ast.NewRule($1, $2, $3, $8, $5), nil >>;

RuleList
  : Rule                              << ast.NewRuleList($0), nil >>
  | RuleList Rule                     << ast.AppendRule($0, $1), nil >>;
